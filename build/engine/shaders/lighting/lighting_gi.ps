
/*
lighting_gi.ps

*/

#include <lpv.h>
#include <struct.h>
#include <mtrl.h>

//##########################################################################

cbuffer perFrame: register(b1)
{
	float3 g_vViewPos;
};

//##########################################################################

SamplerState g_sPointClamp: register(s0);
SamplerState g_sLinearClamp: register(s1);

Texture2D g_txDepth:register(t0);
Texture2D g_txNormals:register(t1);
Texture3D g_txLightVolumeRed:register(t2);
Texture3D g_txLightVolumeGreen:register(t3);
Texture3D g_txLightVolumeBlue:register(t4);

//#############################################################################

PSO_Lbuffer main(VSO_ResPos IN)
{
	PSO_Lbuffer OUT;

	float4 vNormals = g_txNormals.Sample(g_sPointClamp, IN.vTexUV);
	float fDepth = g_txDepth.Sample(g_sPointClamp,IN.vTexUV).r;
	
	float3 vOrigin = float3(0, 0, 0); // Центр сетки
	float4 vPosition = float4(g_vViewPos.xyz + IN.vWorldRay * fDepth, 1.0); // Мировая позиция пиксела
	
	float3 vNormalPixel = normalize(NormalDecode(vNormals.xyz).xyz);
	
	float3 vTexCoord = GetGridTexCoord(vPosition.xyz, 0);
	
	float4 vColorR = g_txLightVolumeRed.Sample(g_sLinearClamp, vTexCoord);
	float4 vColorG = g_txLightVolumeGreen.Sample(g_sLinearClamp, vTexCoord);
	float4 vColorB = g_txLightVolumeBlue.Sample(g_sLinearClamp, vTexCoord);
	
	// float3 vNormalPixel = normalize(IN.vNormal);
	float4 SHintensity = dirToSH(-vNormalPixel);
	
	float r = saturate(dot(SHintensity, vColorR));
	float g = saturate(dot(SHintensity, vColorG));
	float b = saturate(dot(SHintensity, vColorB));
	
	OUT.vAmdient.xyz = float3(r, g, b) / PI/*  * 6.0f */;
	OUT.vAmdient.w = 1;
	OUT.vSpecular = 0;
	
	return OUT;
}





















