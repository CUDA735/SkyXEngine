
#include <../struct.h>
#include <../mtrl.h>

//матрица для трансформации координат текстуры отражения (над водой)
static const half4x4  Mr=
{	0.5,	0,		0,		0, 
    0,		0.5,	0,		0, 
    0,		0,		0.5,	0, 
    0.5,	0.5,	0.5,	1
};

//матрица для трансофрмации координат текстуры преломления (под водой)
static const half4x4  Mr2=
{	0.5,	0,		0,		0, 
    0,		-0.5,	0,		0, 
    0,		0,		0.5,	0, 
    0.5,	0.5,	0.5,	1
};

half2 WinSize;
half4 Param;		
//x - высота волны
//y - глубина
//z - влияние волн на текстуру с мусором
//w - масштабирование координат текстур для основной текстуры

half4 NearFarIsUnlit;			//ближняя и дальнияя плоскости отсечения

sampler2D ColorMap:register(s0);		//основная текстура	
sampler2D DUDVMap:register(s2);			//карта смещений координат
sampler2D NormalMap:register(s6);		//карта нормалей
sampler2D RF0SSIsSampler:register(s10);	//параметры для освещения
sampler2D TexReflection:register(s12);	//карта отражений того что над водой, рефлексия
sampler2D DepthMap:register(s14);		//карта глубины

ps_out_ds_mrt main(vs_out_water IN)
{
	
	ClipFar(IN.Pos.z,NearFarIsUnlit.y);
	
	ps_out_ds_mrt OUT;
	half4 tmpPPos = IN.Pos;
	half Depth = GetDepthW(IN.Pos,NearFarIsUnlit.xy).x;
	half3 colorOne = 2.0f * tex2D(NormalMap, IN.Tex2.xy) - 1.0f;
	half3 colorTwo = 2.0f * tex2D(NormalMap, IN.Tex3.xy) - 1.0f;
	
	colorOne.z = IN.Normal.z;
	colorTwo.z = IN.Normal.z;

	half3 normal = normalize((colorOne+colorTwo+IN.Normal));
    half3 WorldNormal = normal;
	half3 texturecoordoffset = 2.0f * tex2D(DUDVMap,IN.Tex2.xy) - 1.0f;
	half3 texturecoordoffset2= 2.0f * tex2D(DUDVMap,IN.Tex3.xy*0.5) - 1.0f;
	half3 normaloffset = normalize(texturecoordoffset+texturecoordoffset2)*Param.y*10.f;

	half4 tmppsposnontr = mul(IN.Pos,Mr2);
	half depth = tex2D(DepthMap, tmppsposnontr.xy/tmppsposnontr.w + half2(0.5/WinSize.x,0.5/WinSize.y)).r;
	half intens = saturate(abs((Depth - depth)*NearFarIsUnlit.y) / (Param.x * 100.f));
	intens *= intens;
	normaloffset *= intens;
	IN.Pos.xyz += normaloffset;
	
	half4 tmppspos = mul(IN.Pos,Mr2);
	
	half4 colorbase = tex2Dproj(ColorMap, tmppspos);
	
	half4 inTexProj = mul(IN.Pos,Mr);
	half4 reflection = tex2D(TexReflection, inTexProj.xy/inTexProj.w);
	reflection = lerp(reflection,colorbase,0.2);
	
	OUT.Color.rgb = reflection.rgb;
	OUT.Color.a = intens;
	
	OUT.Normal.xyz = 0.5f * WorldNormal + 0.5;
	OUT.Normal.w = NearFarIsUnlit.z;
	
	OUT.Param = half4(tex2D(RF0SSIsSampler,IN.TexUV).rg, tmppspos.xy/tmppspos.w);
	OUT.Param.w = NearFarIsUnlit.w;
	
	OUT.Depth = GetDepthW(tmpPPos,NearFarIsUnlit.xy);
	
	return OUT;
}