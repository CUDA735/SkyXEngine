
/*
mtrlgeom_land_det4.ps
Рендер ландшафта (геометрии), 4 детальные текстуры по маске
*/

#include <../struct.h>
#include <../mtrl.h>

sampler2D BaseTex:register(s0);

sampler2D MaskTex:register(s1);

sampler2D DetTex0:register(s2);
sampler2D DetTex1:register(s3);
sampler2D DetTex2:register(s4);
sampler2D DetTex3:register(s5);

sampler2D ParamTex:register(s10);

half4 Param;
half4 NearFarIsUnlit;

ps_out_ds_mrt main(vs_out_gcommon IN)
{
	ClipFar(IN.Pos.z,NearFarIsUnlit.y);
	ps_out_ds_mrt OUT;
	half2 dx = ddx((IN.TexUV) * Param.x * MTRL_LAND_TEXUV_SCALE);
	half2 dy = ddy((IN.TexUV) * Param.x * MTRL_LAND_TEXUV_SCALE);
	half lod = 9.f+log2(max(length(dx), length(dy)));

	IN.Normal = normalize(IN.Normal);
	half4 basecolor = tex2D(BaseTex, IN.TexUV);
	half4 detmaskcolor = tex2D(MaskTex, IN.TexUV);
		[branch]if(IN.Pos.z < MTRL_LAND_DIST)
		{
			half4 detcolor1 = tex2Dlod(DetTex0, half4((IN.TexUV) * Param.x * MTRL_LAND_TEXUV_SCALE,0.f,lod));
			half4 detcolor2 = tex2Dlod(DetTex1, half4((IN.TexUV) * Param.x * MTRL_LAND_TEXUV_SCALE,0.f,lod));
			half4 detcolor3 = tex2Dlod(DetTex2, half4((IN.TexUV) * Param.x * MTRL_LAND_TEXUV_SCALE,0.f,lod));
			half4 detcolor4 = tex2Dlod(DetTex3, half4((IN.TexUV) * Param.x * MTRL_LAND_TEXUV_SCALE,0.f,lod));
			
			half4 detcolor = detcolor1 * detmaskcolor.r + detcolor2 * detmaskcolor.g + detcolor3 * detmaskcolor.b + detcolor4 * detmaskcolor.a;
			detcolor *= Param.y * 4.f;
			
			half4 blend = basecolor * detcolor * (4.f - detcolor);
			basecolor.rgb = lerp(blend,basecolor,lerpfactor).rgb;
		}	

	OUT.Normal.xy = NormalEncode(IN.Normal);
	OUT.Normal.w = NearFarIsUnlit.z;
	OUT.Normal.z = NearFarIsUnlit.w;
	
	OUT.Param = tex2D(ParamTex,IN.TexUV);
	//OUT.Param.w = NearFarIsUnlit.w;
	OUT.Color = basecolor;
	
	OUT.Depth = GetDepthW(IN.Pos,NearFarIsUnlit.xy);
	return OUT;
}