
/*
mtrlgeom_reflect_cube_mrbase.vs
Рендер геометрии с объемным (кубическим) отражением и микрорельефом
*/

#include <../struct.h>
#include <../mtrl.h>

sampler2D BaseTex:register(s0);
sampler2D MRTex0:register(s6);

sampler2D ParamTex:register(s10);
samplerCUBE ReflectTex:register(s12);

half4 Param;
half4 NearFarIsUnlit;

ps_out_ds_mrt main(vs_out_refcube IN)
{
	ps_out_ds_mrt OUT; 
	OUT.Color = tex2D(BaseTex,IN.TexUV);
	
	half3 NormalMicro = ((2.f * (tex2D(MRTex0, IN.TexUV).rgb)) - 1.f);
	NormalMicro *= Param.z;
	half3 NormalPixel = normalize((IN.Normal+half3(NormalMicro.xy,IN.Normal.z)) * 0.5f);
	
	OUT.Normal.xyz = 0.5f * NormalPixel + 0.5f;
	OUT.Normal.w = NearFarIsUnlit.z;
	
	OUT.Param = tex2D(ParamTex,IN.TexUV);
	OUT.Param.w = NearFarIsUnlit.w;
	
	half MipmapIndex = GetTexLod4Ref(saturate(OUT.Param.r - 0.1f));
	half4 reflection = texCUBElod(ReflectTex, half4(IN.CubePPos,MipmapIndex));
	OUT.Color.xyz = lerp(OUT.Color.xyz,reflection.xyz,OUT.Param.y);
	OUT.Depth = GetDepthW(IN.Pos,NearFarIsUnlit.xy);
	
	return OUT;
}