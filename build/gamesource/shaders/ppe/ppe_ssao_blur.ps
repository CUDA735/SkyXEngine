
/*
ppe_ssao_blur.ps
Размытие ssao (результата просчета) и повторное размытие со смешиванием со сценой.
Размытие с учетом линейной глубины.
*/

#include <../struct.h>
#include <../mtrl.h>

half4 PixelSize;

sampler2D DepthMap:register(s0);
sampler2D BlurMap:register(s1);
sampler2D ScreenMap:register(s2);

half2 NearFar;

half4 main(vs_out_pp IN) : COLOR0
{
	half2 BlurOffset = PixelSize;
	half2 CenterTC = IN.TexUV;

	half SumWeight = 0.125f;
	half4 Color = tex2D(BlurMap,CenterTC) * SumWeight;

	half CenterDepth = tex2D(DepthMap, CenterTC).r * NearFar.y;

		[unroll]
		for(half x=-1;x<=1;x++)
		{
				[unroll]
				for(half y=-1;y<=1;y++)
				{
					half2 SampleTC = CenterTC + half2(x * PixelSize.x,y * PixelSize.y);
					half Depth = tex2D(DepthMap, SampleTC).r;
					Depth *= NearFar.y;
					half Diff = 9.f*(1.f - Depth/CenterDepth);
					half Weight = saturate(0.5f - 0.75f*abs(Diff) - 0.25f*(Diff));
					Color += tex2D(BlurMap,SampleTC) * Weight;
					SumWeight += Weight;
				}
		}

	Color /= SumWeight;
	
	#if defined(_BLEND_COLOR_)
	half4 ScreenColor = tex2D(ScreenMap,IN.TexUV);
	return ScreenColor*saturate(Color);
	#else
	return saturate(Color);
	#endif
}